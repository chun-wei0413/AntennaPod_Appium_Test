*** Settings ***
Library    AppiumLibrary
Library    Collections

*** Keywords ***
Go To Home Page
    ${homeButtonID}=    Set Variable    id=de.danoeh.antennapod:id/bottom_navigation_home
    Click Element    ${homeButtonID}

Go To Queue Page
    ${queueButtonID}=    Set Variable    id=de.danoeh.antennapod:id/bottom_navigation_queue
    Click Element    ${queueButtonID}

Go To Subscription Page
    ${subscriptionButtonID}=    Set Variable    id=de.danoeh.antennapod:id/bottom_navigation_subscriptions
    Click element    ${subscriptionButtonID}

Subscribe Podcast By Index
    [Arguments]    ${index}
    ${podcastGridElement}=    Set Variable    xpath=//android.widget.GridView[@resource-id="de.danoeh.antennapod:id/discover_grid"]/android.widget.LinearLayout[${index}]
    ${suscribeButton}=    Set Variable    xpath=//android.widget.Button[@resource-id="de.danoeh.antennapod:id/butSubscribe"]
    Click Element    ${podcastGridElement}
    Wait Until Element Is Visible    ${suscribeButton}    timeout=10s
    Click Element    ${suscribeButton}

Add Podcast To Subscriptions
    Go To Subscription Page
    Click Element    id=de.danoeh.antennapod:id/subscriptions_add
    Subscribe Podcast By Index    1

Click Button Action In Podcast Information Page
    ${buttonAction}=    Set Variable    xpath=//android.widget.LinearLayout[@resource-id="de.danoeh.antennapod:id/butAction1"]
    Wait Until Element Is Visible   ${buttonAction}   10s
    Click Element    ${buttonAction}

Audio Player Should Be Visible
    ${audioPlayer}=    Set Variable    xpath=//android.widget.LinearLayout[@resource-id="de.danoeh.antennapod:id/fragmentLayout"]
    Wait Until Element Is Visible    ${audioPlayer}    10s

Append Title To List
    [Arguments]    ${title}
    Append To List    ${episodeTitleList}    ${title}
    Set Suite Variable    @{episodeTitleList}    @{episodeTitleList}

Play And Pause Podcast
    [Arguments]    ${episodeIndex}    ${isNeedTitle}=${False}
    ${episode}=    Set Variable    xpath=(//android.widget.LinearLayout[@resource-id="de.danoeh.antennapod:id/container"])[${episodeIndex}]
    Wait Until Element Is Visible    ${episode}    timeout=5s
    ${title}=    Get Text    ${episode}/android.widget.LinearLayout[2]//android.widget.TextView[@resource-id="de.danoeh.antennapod:id/txtvTitle"]
    Click Element    ${episode}
    Click Button Action In Podcast Information Page
    Audio Player Should Be Visible
    Sleep    5s
    Click Button Action In Podcast Information Page
    Run Keyword If    ${isNeedTitle}    Append Title To List    ${title}

Click Back Button In Podcast Information Page
    ${backButton}=    Set Variable    xpath=//android.widget.ImageButton[@content-desc="Back"]
    Click Element    ${backButton}

Click Audio Player
    ${audioPlayer}=    Set Variable    xpath=//android.widget.LinearLayout[@resource-id="de.danoeh.antennapod:id/fragmentLayout"]
    ${player}=    Set Variable    xpath=//android.widget.LinearLayout[@resource-id="de.danoeh.antennapod:id/playtime_layout"]
    Wait Until Element Is Visible    ${audioPlayer}    10s
    Click Element    ${audioPlayer}
    Wait Until Page Contains Element    ${player}    10s

Click Back Button In Podcast Page
    ${backButton}=    Set Variable    xpath=//android.widget.LinearLayout[@resource-id="de.danoeh.antennapod:id/appbar"]//android.view.ViewGroup[@resource-id="de.danoeh.antennapod:id/toolbar"]//android.widget.TextView[@text="Home"]
    Wait Until Element Is Visible   ${backButton}   10s
    Click Element    ${backButton}